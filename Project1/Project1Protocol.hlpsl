%Security protocol used for change of keys
% Message 1. A + S: A, B.
% Message 2. S -+ A: (T,, L, Kb, B, ITS, L, &, AIK,,~JK~.
% Message 3. A +B: IT,,& &~,A]K~~, M, Ta)~,,,.
% Message 4. B -+A: (T= + lJKab. 

role alice (A,B,S : agent,
            K_AS : symmetric_key,
            Snd,Rcv : channel(dy)) 
        
played_by A def=     

    local State: nat,
          Na,Nb: text,
          K_AB, K_BS:symmetric_key

    init State:=0

    transition
        1. State=0 /\ Rcv(start)
            =|> State':=1 /\ Snd({A.B}_K_AS)
        
        2. State=1 /\ Rcv({K_AB'.B.{K_AB'.A}_K_BS}_K_AS)
            =|> State':=2 /\ Snd({K_AB'.A}_K_BS)
                          /\ Na':=new() 
                          /\ Snd({Na'.A}_K_AB')
                          /\ witness(A,B,alice_bob_na,Na')
                          /\ secret(Na', sna, {A,B})

        3. State=2 /\ Rcv({Nb'.B}_K_AB')
            =|> State':=3 /\ request (A,B,bob_alice_nb,Nb')
                          /\ wrequest (A,B,bob_alice_nb,Nb')
end role

role bob (B,A: agent,
          K_BS: symmetric_key,
          Snd,Rcv: channel(dy)) 

played_by B def=

    local State: nat,
          Na,Nb: text,
          K_AB: symmetric_key

    init State:=0

    transition
        1. State=0 /\ Rcv({K_AB'.A}_K_BS)
            =|> State':=1 
            
        2. State=1 /\ Rcv({Na'.A}_K_AB')    
            =|> State':=2  /\ Nb':=new()
                           /\ Snd({Nb'.B}_K_AB')
                           /\ witness(B,A,bob_alice_nb,Nb')
                           /\ request (B,A,alice_bob_na,Na')
                           /\ wrequest (B,A,alice_bob_na,Na')
                           /\ secret(Nb', snb, {A,B})

end role

role server (S: agent,
             K_AS,K_BS: symmetric_key,
             Snd, Rcv: channel(dy))
              
played_by S def=
    
    local A,B : agent,
          K_AB : symmetric_key

    transition
     1. Rcv({A.B}_K_AS)
        =|> K_AB':=new() /\ Snd({K_AB'.B.{K_AB'.A}_K_BS}_K_AS)
        
end role

role securityProtocol(A,B,S: agent,
                      K_AS,K_BS: symmetric_key,
                      Snd,Rcv : channel(dy)) def=
    
    composition
        server(S,K_AS,K_BS,Snd,Rcv) /\
        alice(A,B,S,K_AS,Snd,Rcv) /\
        bob(B,A,K_BS,Snd,Rcv) 
                      
end role

role environment() def=

    local Snd, Rcv: channel(dy)

    const a, b, s, i: agent,
          k_as, k_bs, k_is: symmetric_key,
          ki: public_key,
          sna,snb,alice_bob_na, bob_alice_nb: protocol_id

    intruder_knowledge = {a,b,s,i,k_is,inv(ki)}

    composition
        securityProtocol(a,b,s,k_as,k_bs,Snd,Rcv) /\
        securityProtocol(a,i,s,k_as,k_is,Snd,Rcv) /\    
        securityProtocol(i,b,s,k_is,k_bs,Snd,Rcv) 

end role

goal
    authentication_on alice_bob_na,bob_alice_nb
    weak_authentication_on alice_bob_na,bob_alice_nb
    secrecy_of sna,snb
end goal

environment()